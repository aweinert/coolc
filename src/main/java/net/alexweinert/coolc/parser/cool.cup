/*
 * cool.cup
 *
 * CS164: Spring 2004 
 * Programming Assignment 3
 *
 * Parser definition for the Cool language.
 *
 */

import java_cup.runtime.*;

/* Code enclosed in {: :} is copied verbatim to the class containing
all parser actions.  All the extra variables/functions you want to use
in the semantic actions should go here.  Don't remove or modify anything
that was there initially.  */

action code {: 

    int curr_lineno() {
	return ((CoolTokenLexer)parser.getScanner()).curr_lineno();
    }

    AbstractSymbol curr_filename() {
	return ((CoolTokenLexer)parser.getScanner()).curr_filename();
    }

:} 

/************************************************************************/
/*                DON'T CHANGE ANYTHING IN THIS SECTION                 */

parser code {:
    int omerrs = 0;

    public void syntax_error(Symbol cur_token) {
        int lineno = action_obj.curr_lineno();
	String filename = action_obj.curr_filename().getString();
        System.err.print("\"" + filename + "\", line " + lineno + 
		         ": parse error at or near ");
        Utilities.printToken(cur_token);
	omerrs++;
	if (omerrs>50) {
	   System.err.println("More than 50 errors");
	   System.exit(1);
	}
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
    }
:}


/* Declare the terminals; a few have types for associated lexemes.  The
token ERROR is never used in the parser; thus, it is a parse error when
the lexer returns it.  */
terminal CLASS, ELSE, FI, IF, IN, INHERITS, LET, LET_STMT, LOOP, POOL, THEN, WHILE;
terminal CASE, ESAC, OF, DARROW, NEW, ISVOID;
terminal ASSIGN, NOT, LE, ERROR;
terminal PLUS, DIV, MINUS, MULT, EQ, LT, DOT, NEG, COMMA, SEMI, COLON;
terminal LPAREN, RPAREN, AT, LBRACE, RBRACE;
terminal AbstractSymbol STR_CONST, INT_CONST;
terminal Boolean BOOL_CONST;
terminal AbstractSymbol TYPEID, OBJECTID;

/*  DON'T CHANGE ANYTHING ABOVE THIS LINE, OR YOUR PARSER WON'T WORK      */
/**************************************************************************/


/*
 * Declare types for the grammar's non-terminals.
 *
 * Hint: You will have to add/modify this list.
 */
nonterminal programc program;
nonterminal Classes class_list;
nonterminal class_c class;
nonterminal Features feature_list;
nonterminal Feature feature;
nonterminal Expression expr, opt_init, nested_let, let_expression;
nonterminal Expressions actuals, exp_list, block_body;
nonterminal Formals formals;
nonterminal Formal formal;
nonterminal Case case;
nonterminal Cases cases;
nonterminal AbstractSymbol opt_inherits;


/*
 * Precedence declarations.
 */
precedence right ASSIGN;
precedence left NOT;
precedence left LE, LT, EQ;
precedence left PLUS, MINUS;
precedence left MULT, DIV;
precedence left ISVOID;
precedence left NEG;
precedence left AT;
precedence left DOT;

/***********************************************************************
 * Grammar rules.                                                      *
 *                                                                     *
 * Some parts of rules have been provided for you as an example and    *
 * to get you started.  You should not need to modify this code to get *
 * a working solution, but you are welcome to if you like.  However,   *
 * do not assume that any particular rule is complete.                 *
 ***********************************************************************/

/* Program */
program	
	/* A program is nothing but a list of classes. Errors are handled in the 
	 * class_list nonterminal */
	::= class_list:cl
	    {: RESULT = new programc(curr_lineno(), cl); :}
	;


class_list
	/* Base case, start with empty class list */
	::=
	    {: RESULT = (new Classes(curr_lineno())); :}
	/* Append a class to the current list of classes */
	| class_list:cl class:c SEMI 
	    {: RESULT = cl.appendElement(c); :}
	/* Error in a class definition, jump over it */
	| class_list:cl error SEMI
		{: RESULT = cl; :}
	;


/* Class declarations */
class
	::= CLASS TYPEID:n opt_inherits:inherits LBRACE feature_list:def RBRACE
	    {: RESULT = new class_c(curr_lineno(), n, inherits, def, curr_filename()); :}
	;

/* Optional inherits-declaration */
opt_inherits
	::= /* No inherits declaration */
		{: RESULT = AbstractTable.idtable.addString("Object"); :}
	/* Actual inherits declaration */
	| INHERITS TYPEID:typeid
		{: RESULT = typeid; :}
	;

/* The list of features that make up the class */
feature_list
	/* Base case, start with empty list */
	::= 
		{: RESULT = (new Features(curr_lineno())); :}
	/* Add a new feature to the list */
	| feature_list:fl feature:f SEMI
		{: RESULT = fl.appendElement(f); :}
	/* If there is an error while parsing a feature, ignore it */
	| feature_list:fl error SEMI
		{: RESULT = fl; :}
	;

/* The actual feature, may be an attribute or a method */
feature
	/* Parse a method */
	::= OBJECTID:name formals:args COLON TYPEID:return_type LBRACE expr:body RBRACE
		{: RESULT = new method(curr_lineno(), name, args, return_type, body); :}
	/* Parse an attribute, with optional initialization */
	| OBJECTID:name COLON TYPEID:type opt_init:init
		{: RESULT = new attr(curr_lineno(), name, type, init); :};

/* Optional initialization */
opt_init
	/* Actual initialization */
	::= ASSIGN expr:init
		{: RESULT = init; :}
	/* Missing initialization, initialize with no_expr */
	|
		{: RESULT = new no_expr(curr_lineno()); :}
		;

/* The set of all formal parameters of a method */
formals
	/* No arguments */
	::= LPAREN RPAREN
	    {: RESULT = new Formals(curr_lineno()); :}
	/* List of names */
	| LPAREN formal:f
		{: RESULT = (new Formals(curr_lineno())).appendElement(f); :}
	/* Add a new formal to the list */
	| formals:fl COMMA formal:f
		{: RESULT = fl.appendElement(f); :}
	/* Finish the list off */
	| formals:fl RPAREN
		{: RESULT = fl; :}
	;

/* A single formal parameter of a method */
formal
	::= OBJECTID:name COLON TYPEID:type
		{: RESULT = new formalc(curr_lineno(), name, type); :};

/* Expressions */
expr
	/* Arithmetic expressions */
	::= expr:e1 PLUS expr:e2
	    {: RESULT = new plus(curr_lineno(), e1, e2); :}
	| expr:e1 MINUS expr:e2
	    {: RESULT = new sub(curr_lineno(), e1, e2); :}
	| expr:e1 MULT expr:e2
	    {: RESULT = new mul(curr_lineno(), e1, e2); :}
	| expr:e1 DIV expr:e2
	    {: RESULT = new divide(curr_lineno(), e1, e2); :}
	| NEG expr:e
	    {: RESULT = new neg(curr_lineno(), e); :}
	| expr:e1 LT expr:e2
	    {: RESULT = new lt(curr_lineno(), e1, e2); :}
	| expr:e1 EQ expr:e2
	    {: RESULT = new eq(curr_lineno(), e1, e2); :}
	| expr:e1 LE expr:e2
	    {: RESULT = new leq(curr_lineno(), e1, e2); :}
	| NOT expr:e
	    {: RESULT = new comp(curr_lineno(), e); :}
	| LPAREN expr:e RPAREN
	    {: RESULT = e; :}

	/* Statements */
	| OBJECTID:lhs ASSIGN expr:rhs
		{: RESULT = new assign(curr_lineno(), lhs, rhs); :}
	| NEW TYPEID:type
		{: RESULT = new new_(curr_lineno(), type); :}
	| ISVOID expr:expr
		{: RESULT = new isvoid(curr_lineno(), expr); :}

	/* Control structures */
	| CASE expr:expr OF cases:cases ESAC
		{: RESULT = new typcase(curr_lineno(), expr, cases); :}
	| IF expr:cond_expr THEN expr:then_expr ELSE expr:else_expr FI
		{: RESULT = new cond(curr_lineno(), cond_expr, then_expr, else_expr); :}
	| WHILE expr:cond LOOP expr:body POOL
		{: RESULT = new loop(curr_lineno(), cond, body); :}
	| let_expression:let /* Factor out let expression to give lowest precedence to let-expression */
		{: RESULT = let; :}
	| LBRACE block_body:body RBRACE
		{: RESULT = new block(curr_lineno(), body); :}

	/* Method calls */
	| OBJECTID:n actuals:a /* Call on self */
	    {: RESULT = new dispatch(curr_lineno(), new object(curr_lineno(), AbstractTable.idtable.addString("self")), n, a); :}
	| expr:expr DOT OBJECTID:objid actuals:actuals /* Call on variable */
		{: RESULT = new dispatch(curr_lineno(), expr, objid, actuals); :}
	| expr:expr AT TYPEID:type DOT OBJECTID:objid actuals:actuals /* Call on casted variable */
		{: RESULT = new static_dispatch(curr_lineno(), expr, type, objid, actuals); :}

	/* Constants */
	| INT_CONST:i
	    {: RESULT = new int_const(curr_lineno(), i); :}
	| STR_CONST:s
	    {: RESULT = new string_const(curr_lineno(), s); :}
	| BOOL_CONST:b
	    {: RESULT = new bool_const(curr_lineno(), b); :}
	| OBJECTID:o
	    {: RESULT = new object(curr_lineno(), o); :}
	;

/* Body of a block of expressions */
block_body
	::= /* Base case, create new, empty list */
		{: RESULT = new Expressions(curr_lineno()); :}
	| block_body:el expr:e1 SEMI /* Add a new expression to the block */
		{: RESULT = el.appendElement(e1); :}
	| block_body:el error SEMI /* If we have a malformed expression, just skip it */
		{: RESULT = el; :}
	;

/* Let expression, is immediatly desugared to nested let-expressions with one variable each */
let_expression
	::= LET nested_let:nested_let
		{: RESULT = nested_let; :}
	;

nested_let
	/* Base case, last let-statement in front of the actual body */
	::= OBJECTID:new_var COLON TYPEID:type opt_init:init IN expr:body
		{: RESULT = new let(curr_lineno(), new_var, type, init, body); :}
	/* Add another let-statement to the nested statements */
	| OBJECTID:new_var COLON TYPEID:type opt_init:init COMMA nested_let:inner
		{: RESULT = new let(curr_lineno(), new_var, type, init, inner); :}
	| error IN expr:body /* Error in base case, skip it */
		{: RESULT = body; :}
	| error COMMA nested_let:inner /* Error in nested case, skip it */
		{: RESULT = inner; :}
	;

/* The whole set of case-branches */
cases
	::= case:new_case /* Base case, create new empty list of branches */
		{: 	Cases new_cases = new Cases(curr_lineno());
			RESULT = new_cases.appendElement(new_case); :}
	| cases:old_cases case:new_case /* Add a new branch to the list */
		{: 	RESULT = old_cases.appendElement(new_case); :}
	;

/* A single branch of a case-statement */
case
	::= OBJECTID:objectid COLON TYPEID:type DARROW expr:expr SEMI
		{: RESULT = new branch(curr_lineno(), objectid, type, expr); :}
	;


/* Method arguments */
actuals
	/* No arguments */
	::= LPAREN RPAREN
	    {: RESULT = new Expressions(curr_lineno()); :}
	/* List of arguments */
	| LPAREN exp_list:el RPAREN
	    {: RESULT = el; :}
	;

/* Non-empty list of expressions */
exp_list
	/* One expression */
	::= expr:e
	    {: RESULT = (new Expressions(curr_lineno())).appendElement(e); :} 
	/* Several expressions */
	| exp_list:el COMMA expr:e
	    {: RESULT = el.appendElement(e); :}
	;
